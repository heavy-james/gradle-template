//=============build aar below==========================
apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

//================publish with source code below===============

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}


publishing {
    publications {
        dialog(MavenPublication) {
            groupId rootProject.ext.groupId
            artifactId rootProject.ext.artifactId
            version rootProject.ext.publishVersion
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/dialog-release.aar")
        }
    }
    repositories {
        maven {
            credentials {
                username rootProject.ext.userName
                password rootProject.ext.passWord
            }
            url "$rootProject.ext.repoUrl"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$rootProject.ext.appCompatV7"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
