ext {
    //设置App配置
    setAppDefaultConfig = {
        extension ->
            extension.apply plugin: 'com.android.application'
            extension.description "app"
            setAndroidConfig extension.android
            setDependencies extension.dependencies
            extension.android.defaultConfig {//设置applicationId
                applicationId rootProject.ext.android.applicationId + "." + extension.getName()
            }

            extension.android.dataBinding {
                enabled = true
            }
    }

    //设置Lib配置
    setLibDefaultConfig = {
        extension ->
            extension.apply plugin: 'com.android.library'
            extension.description "lib"
            setAndroidConfig extension.android
            setDependencies extension.dependencies

            extension.android.dataBinding {
                enabled = true
            }
    }

    //设置Android配置
    setAndroidConfig = {
        extension ->
            extension.compileSdkVersion rootProject.ext.android.compileSdkVersion
            extension.defaultConfig {
                minSdkVersion rootProject.ext.android.minSdkVersion
                targetSdkVersion rootProject.ext.android.targetSdkVersion
                testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [AROUTER_MODULE_NAME: extension.project.getName()]
                    }
                }
            }
    }

    //设置依赖
    setDependencies = {
        extension ->
            extension.implementation fileTree(dir: 'libs', include: ['*.jar'])
            extension.testImplementation 'junit:junit:4.12'
            extension.androidTestImplementation 'com.android.support.test:runner:1.0.2'
            extension.androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
            extension.api "android.arch.lifecycle:extensions:$rootProject.ext.archLifecycleVersion"
            extension.annotationProcessor "android.arch.lifecycle:compiler:$rootProject.ext.archLifecycleVersion"
            extension.annotationProcessor rootProject.ext.dependencies["arouter"]["arouter-compiler"]

    }
}